plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.0'
	id 'io.spring.dependency-management' version '1.1.7'
	// id 'org.graalvm.buildtools.native' version '0.11.3' // in combination with spring-boot 4 this requires java_home 25 and graalvm_home 25 (java 21 in build.gradle is fine)
}

group = 'net.code_notes'
// NOTE: will replace first occurence in Dockerfile
version = 'VERSION'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.0'

    implementation 'com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20240325.1'
	implementation 'io.swagger.core.v3:swagger-core-jakarta:2.2.41'

	compileOnly 'org.projectlombok:lombok'
    // remove warning caused by @Nullable
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2' 
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	annotationProcessor 'org.projectlombok:lombok'

    testCompileOnly 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    // for ci testing
	testRuntimeOnly 'com.h2database:h2'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

tasks.named('test') {
	useJUnitPlatform()
}

test {
    // show application logs (like runtime logs) during tests
    testLogging {
        showStandardStreams = true
    }

    // use arg "-Pci"
    if (project.hasProperty("ci"))
        systemProperty 'CI', 'true';
}

bootRun {
    // e.g. ./gradlew bootRun -PjavaRuntimeArgs="-Xmx1g -Xms256m"
    if (project.hasProperty('javaRuntimeArgs') && project.javaRuntimeArgs.trim() != '')
        jvmArgs = (project.javaRuntimeArgs.split("\\s+") as List)
}